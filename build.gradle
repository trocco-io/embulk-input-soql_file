plugins {
    id "java"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "com.diffplug.spotless" version "5.15.0"
    id "com.palantir.git-version" version "0.13.0"
}

repositories {
    mavenCentral()
}
configurations {
    provided
}

group = "io.trocco"
description = "An embulk input plugin to load records from Salesforce."
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^v[0-9]+\.[0-9]+\.[0-9]+(\.[a-zA-Z0-9]+)?/) {
        vd.lastTag.substring(1)
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    def embulkVersion = "0.10.36"

    compileOnly "org.embulk:embulk-api:${embulkVersion}"
    compileOnly "org.embulk:embulk-spi:${embulkVersion}"

    compile "org.embulk:embulk-util-file:0.1.5"

    compile "com.force.api:force-wsc:47.0.0"
    compile "com.force.api:force-partner-api:47.0.0"

    testImplementation "junit:junit:4.+"
}

embulkPlugin {
    mainClass = "org.embulk.input.soql.SoqlFilePlugin"
    category = "input"
    type = "soql_file"
}

clean { delete "classpath" }

gem {
    from("LICENSE.txt")
    authors = [ "dododo8m" ]
    email = [ "taichiddt+8m@gmail.com" ]
    summary = "An embulk input plugin to load records from Salesforce."
    homepage = "https://github.com/trocco-io/embulk-input-soql_file"
    licenses = [ "MIT" ]
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat().aosp()
    }
}
